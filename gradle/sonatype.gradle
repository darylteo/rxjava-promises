apply plugin: 'maven'
apply plugin: 'signing'

def release = { !project.version.endsWith('-SNAPSHOT') }
def sonatypeUsername = { project.hasProperty('sonatypeUsername') ? project.sonatypeUsername : '' }
def sonatypePassword = { project.hasProperty('sonatypePassword') ? project.sonatypePassword : '' }

configurations { archives }

task sourcesJar(type: Jar) {
  from sourceSets.main.allSource
  classifier = 'sources'
}

task javadocJar(type: Jar) {
  from javadoc
  classifier = 'javadoc'
}

artifacts {
  archives sourcesJar
  archives javadocJar
}

signing {
  required { release() && gradle.taskGraph.hasTask(uploadArchives) }
  sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      afterEvaluate {
        def url = release() ? 'https://oss.sonatype.org/service/local/staging/deploy/maven2/' : 'https://oss.sonatype.org/content/repositories/snapshots/'

        repository(url: url) {
          authentication(userName: sonatypeUsername(), password: sonatypePassword())
        }
      }

      uniqueVersion = false

      beforeDeployment { dep ->
        signing.signPom(dep)
      }

      configuration = configurations.archives
    }
  }
}